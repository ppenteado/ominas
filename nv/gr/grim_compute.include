;===============================================================================
; grim_compute_planet_center
;
;===============================================================================
function grim_compute_planet_center, map=map, fov=fov, hide=hide, $
                              gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                              data=data, $
                              npoints=npoints, xds=xds

 grim_message, /clear

 pds = cor_select(active_xds, 'PLANET', /class)
 if(NOT keyword_set(pds)) then pds = gd.pd

 ;--------------------------------
 ; compute points
 ;--------------------------------
; center_ptd = pg_center(cd=gd.cd, bx=pds, fov=fov)
 center_ptd = pg_center(cd=gd.cd, bx=pds)
 grim_message

 xds = pds

 return, center_ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_planet_center
;
;===============================================================================
function grim_symsize_planet_center, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_planet_center
;
;===============================================================================
function grim_shade_planet_center, data, ptd
 return, 1.0
end
;===============================================================================



;===============================================================================
; grim_compute_limb
;
;===============================================================================
function grim_compute_limb, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 if(keyword_set(gd.od)) then od = gd.od
 grim_message, /clear

 pds = cor_select(active_xds, 'GLOBE', /class)
 if(NOT keyword_set(pds)) then pds = gd.pd

 npd = n_elements(pds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 limb_ptd = pg_limb(cd=gd.cd, gbx=pds, od=od, fov=fov, npoints=npoints)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
 if(keyword_set(gd.rd)) then $
   pg_hide, limb_ptd, cd=gd.cd, dkx=gd.rd, od=od, /disk, gbx=gd.pd, hide_ptd_disk, /cat
 grim_message
 if(hide) then hide_ptd_disk = objarr(npd)

 if(keyword_set(gd.sund)) then $
     pg_hide, limb_ptd, cd=gd.cd, gbx=pds, od=gd.sund, /limb, hide_ptd_limb, /cat
 grim_message

 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 if(NOT keyword_set(hide_ptd_disk)) then hide_ptd_disk = obj_new()
 if(NOT keyword_set(hide_ptd_limb)) then hide_ptd_limb = obj_new()

 ptd = [limb_ptd, hide_ptd_limb, hide_ptd_disk]
 xds = [pds, pds, pds]

 return, ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_limb
;
;===============================================================================
function grim_symsize_limb, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_limb
;
;===============================================================================
function grim_shade_limb, data, ptd

 desc = pnt_desc(ptd)
 nptd = n_elements(ptd)

 w = where(strpos(desc, 'hide_limb') NE -1)
 ww = where(strpos(desc, 'hide_disk') NE -1)

 shade = make_array(nptd, val=1.0)
 if(w[0] NE -1) then shade[w] = 0.5
 if(ww[0] NE -1) then shade[ww] = 0.1

 return, shade
end
;===============================================================================



;===============================================================================
; grim_compute_terminator
;
;===============================================================================
function grim_compute_terminator, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 if(NOT keyword_set(gd.sund)) then return, 0

 grim_message, /clear

 pds = cor_select(active_xds, 'PLANET', /class)
 if(NOT keyword_set(pds)) then pds = gd.pd

 npd = n_elements(pds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 term_ptd = pg_limb(cd=gd.cd, od=gd.sund, gbx=pds, fov=fov, npoints=npoints)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
 if(NOT map) then pg_hide, term_ptd, cd=gd.cd, gbx=pds, /limb, hide_ptd_limb, /cat
 if(hide) then hide_ptd_limb = objarr(npd)

 if(keyword_set(gd.rd)) then $
   pg_hide, term_ptd, cd=gd.cd, gbx=pds, dkx=gd.rd, /disk, hide_ptd_disk, /cat
 grim_message
 if(hide) then hide_ptd_disk = objarr(npd)

 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 if(NOT keyword_set(hide_ptd_disk)) then hide_ptd_disk = obj_new()
 if(NOT keyword_set(hide_ptd_limb)) then hide_ptd_limb = obj_new()

 ptd = [term_ptd, hide_ptd_limb, hide_ptd_disk]
 xds = [pds, pds, pds]

 return, ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_terminator
;
;===============================================================================
function grim_symsize_terminator, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_terminator
;
;===============================================================================
function grim_shade_terminator, data, ptd

 desc = pnt_desc(ptd)
 nptd = n_elements(ptd)

 w = where(strpos(desc, 'hide_limb') NE -1)
 ww = where(strpos(desc, 'hide_disk') NE -1)

 shade = make_array(nptd, val=1.0)
 if(w[0] NE -1) then shade[w] = 0.5
 if(ww[0] NE -1) then shade[ww] = 0.5

 return, shade
end
;===============================================================================



;=============================================================================
; grim_compute_planet_grid
;
;=============================================================================
function grim_compute_planet_grid, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 if(keyword_set(gd.od)) then od = gd.od
 grim_message, /clear

 pds = cor_select(active_xds, 'GLOBE', /class)
 if(NOT keyword_set(pds)) then pds = gd.pd

 npd = n_elements(pds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 grid_ptd = pg_grid(cd=gd.cd, bx=pds, fov=fov, npoints=npoints)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
 if(NOT map) then $
  begin
   pg_hide, grid_ptd, cd=gd.cd, od=od, gbx=pds, /limb, hide_ptd_limb, /cat
   if(hide) then hide_ptd_limb = objarr(npd)

   if(keyword_set(gd.rd)) then $
     pg_hide, grid_ptd, cd=gd.cd, dkx=gd.rd, od=od, /disk, gbx=gd.pd, hide_ptd_disk, /cat
   grim_message
   if(hide) then hide_ptd_disk = objarr(npd)

   if(keyword_set(gd.sund)) then $
       pg_hide, grid_ptd, cd=gd.cd, gbx=pds, od=gd.sund, /limb, hide_ptd_term, /cat
   grim_message

  end
 grim_message


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 ptd = grid_ptd
 if(keyword_set(hide_ptd_term)) then ptd = [ptd, hide_ptd_term]
 if(keyword_set(hide_ptd_disk)) then ptd = [ptd, hide_ptd_disk]
 if(keyword_set(hide_ptd_limb)) then ptd = [ptd, hide_ptd_limb]

 xds = pds
 if(keyword_set(hide_ptd_term)) then xds = [xds, pds]
 if(keyword_set(hide_ptd_disk)) then xds = [xds, pds]
 if(keyword_set(hide_ptd_limb)) then xds = [xds, pds]

 return, ptd
end
;=============================================================================



;===============================================================================
; grim_symsize_planet_grid
;
;===============================================================================
function grim_symsize_planet_grid, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_planet_grid
;
;===============================================================================
function grim_shade_planet_grid, data, ptd

 desc = pnt_desc(ptd)
 nptd = n_elements(ptd)

 w = where(strpos(desc, 'hide_limb') NE -1)
 ww = where(strpos(desc, 'hide_disk') NE -1)

 shade = make_array(nptd, val=1.0)
 if(w[0] NE -1) then shade[w] = 0.5
 if(ww[0] NE -1) then shade[ww] = 0.1

 return, shade
end
;===============================================================================



;=============================================================================
; grim_compute_station
;
;=============================================================================
function grim_compute_station, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, npoints=npoints, xds=xds

 if(keyword_set(gd.od)) then od = gd.od
 grim_message, /clear

 ;--------------------------------------------------------
 ; determine primary
 ;--------------------------------------------------------
 if(NOT map) then $
  begin
   pds = cor_select(active_xds, 'PLANET', /class)
   if(NOT keyword_set(pds)) then bx = gd.pd $
   else bx = pds
  end


 stds = gd.std
 if(NOT keyword_set(stds)) then return, 0

 if(keyword_set(bx)) then $
  begin
   for i=0, n_elements(stds)-1 do $
     if((where(cor_name(stn_primary(stds[i])) EQ cor_name(bx)))[0] NE -1) then select = append_array(select, i, /def)
   stds = stds[select]
  end

 nstd = n_elements(stds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 station_ptd = pg_station(cd=gd.cd, std=stds, bx=bx, fov=fov)
 grim_message


 ;--------------------------------
 ; hide points
 ;--------------------------------
 if(NOT map) then $
  begin
   pg_hide, station_ptd, cd=gd.cd, gbx=bx, /globe, hide_ptd_globe, /cat, /one
   grim_message
   if(hide) then hide_ptd_globe = objarr(nstd)
  end
 grim_message

; if(keyword_set(gd.rd)) then $
;   pg_hide, station_ptd, cd=gd.cd, dkx=gd.rd, od=od, /disk, gbx=gd.pd, hide_ptd_disk, /cat, /one
; grim_message
; if(hide) then hide_ptd_disk = objarr(nstd)


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 ptd = station_ptd
 if(keyword_set(hide_ptd_globe)) then ptd = [ptd, hide_ptd_globe]
 if(keyword_set(hide_ptd_disk)) then ptd = [ptd, hide_ptd_disk]

 xds = stds
 if(keyword_set(hide_ptd_globe)) then xds = [xds, stds]
 if(keyword_set(hide_ptd_disk)) then xds = [xds, stds]

 return, ptd
end
;=============================================================================



;===============================================================================
; grim_symsize_station
;
;===============================================================================
function grim_symsize_station, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_station
;
;===============================================================================
function grim_shade_station, data, ptd
 return, 1.0
end
;===============================================================================



;=============================================================================
; grim_compute_array
;
;=============================================================================
function grim_compute_array, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 if(keyword_set(gd.od)) then od = gd.od
 grim_message, /clear

 ;--------------------------------------------------------
 ; determine primary
 ;--------------------------------------------------------
 if(NOT map) then $
  begin
   pds = cor_select(active_xds, 'PLANET', /class)
   if(NOT keyword_set(pds)) then bx = gd.pd $
   else bx = pds
  end

 ards = gd.ard
 if(NOT keyword_set(ards)) then return, 0

 if(keyword_set(bx)) then $
  begin
   for i=0, n_elements(ards)-1 do $
     if((where(cor_name(arr_primary(ards[i])) EQ cor_name(bx)))[0] NE -1) then select = append_array(select, i, /def)
   ards = ards[select]
  end

 nard = n_elements(ards)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 array_ptd = pg_array(cd=gd.cd, ard=ards, bx=bx, fov=fov)
 grim_message


 ;--------------------------------
 ; hide points
 ;--------------------------------
 if(NOT map) then $
  begin
   pg_hide, array_ptd, cd=gd.cd, gbx=bx, /globe, hide_ptd_globe, /cat, /one
   grim_message
   if(hide) then hide_ptd_globe = objarr(nard)
  end
 grim_message

; if(keyword_set(gd.rd)) then $
;   pg_hide, array_ptd, cd=gd.cd, dkx=gd.rd, od=od, /disk, gbx=gd.pd, hide_ptd_disk, /cat, /one
; grim_message
; if(hide) then hide_ptd_disk = objarr(npd)


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 ptd = array_ptd
 if(keyword_set(hide_ptd_globe)) then ptd = [ptd, hide_ptd_globe]
 if(keyword_set(hide_ptd_disk)) then ptd = [ptd, hide_ptd_disk]

 xds = ards
 if(keyword_set(hide_ptd_globe)) then xds = [xds, ards]
 if(keyword_set(hide_ptd_disk)) then xds = [xds, ards]

 return, ptd
end
;=============================================================================



;===============================================================================
; grim_symsize_array
;
;===============================================================================
function grim_symsize_array, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_array
;
;===============================================================================
function grim_shade_array, data, ptd
 return, 1.0
end
;===============================================================================



;===============================================================================
; grim_compute_star
;
;===============================================================================
function grim_compute_star, map=map, fov=fov, hide=hide, $
                            gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                            data=data, $
                            npoints=npoints, xds=xds

 grim_message, /clear

 sds = gd.sd
 nsd = n_elements(sds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 if(NOT keyword_set(sds)) then return, 0
 star_ptd = pg_center(cd=gd.cd, bx=sds, fov=fov)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
;;;; if(keyword__set(gd.rd)) then $
;;;;                  pg_hide, star_ptd, cd=gd.cd, dkx=gd.rd, gbx=gd.pd, /disk
;;;; grim_message

 if(keyword_set(gd.pd)) then pg_hide, star_ptd, cd=gd.cd, gbx=gd.pd, /globe, hide_ptd_globe, /cat
 grim_message

 if(hide) then hide_ptd_globe = objarr(nsd)


 ;--------------------------------
 ; store some data
 ;--------------------------------
 if(keyword_set(data)) then _data = data $
 else _data = {__str_data, name_p:ptr_new(0), mag_p:ptr_new(0)}
 
 *_data.name_p = cor_name(sds)
 *_data.mag_p = str_get_mag(sds)
 data = _data


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 if(NOT keyword_set(hide_ptd_globe)) then hide_ptd_globe = obj_new()

 ptd = [star_ptd, hide_ptd_globe]
 xds = [sds, sds]

 return, ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_star
;
;===============================================================================
function grim_symsize_star, data
 max = max(*data.mag_p) + 2
 return, (max - *data.mag_p) / max
end
;===============================================================================



;===============================================================================
; grim_shade_star
;
;===============================================================================
function grim_shade_star, data, ptd
 max = max(*data.mag_p) + 2
 return, (max - *data.mag_p) / max
end
;===============================================================================



;===============================================================================
; grim_compute_shadow
;
;===============================================================================
function grim_compute_shadow, map=map, fov=fov, hide=hide, $
                     gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                     data=data, $
                     npoints=npoints, xds=xds

 if(map) then return, 0
 if(NOT keyword__set(active_ptd)) then return, 0


 grim_message, /clear

 ;--------------------------------
 ; compute points
 ;--------------------------------
 shadow_ptd = pg_shadow(active_ptd, epsilon=1d, /nocull, $
                         cd=gd.cd, od=gd.sund, dkx=gd.rd, gbx=gd.pd, fov=fov)
 nshad = n_elements(shadow_ptd)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
 pg_hide, shadow_ptd, cd=gd.cd, gbx=gd.pd, dkx=gd.rd, /disk, /globe, hide_ptd, /cat
 grim_message
 if(hide) then hide_ptd = objarr(nshad)

 xds = active_ptd

 ;-------------------------------------------------------------------
 ; note we don't care about dimensions because shadows are not
 ; associated with any objects and they are replaced each time
 ; they are recomputed
 ;-------------------------------------------------------------------
; return, [shadow_ptd, hide_ptd]
 return, shadow_ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_shadow
;
;===============================================================================
function grim_symsize_shadow, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_shadow
;
;===============================================================================
function grim_shade_shadow, data, ptd
 return, 1.0
end
;===============================================================================



;===============================================================================
; grim_compute_reflection
;
;===============================================================================
function grim_compute_reflection, map=map, fov=fov, hide=hide, $
                     gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                     data=data, $
                     npoints=npoints, xds=xds

 if(map) then return, 0
 if(NOT keyword__set(active_ptd)) then return, 0


 grim_message, /clear

 ;--------------------------------
 ; compute points
 ;--------------------------------
 reflection_ptd = pg_reflection(active_ptd, /nocull, $
                         cd=gd.cd, od=gd.sund, dkx=gd.rd, gbx=gd.pd, fov=fov)
 nref = n_elements(reflection_ptd)
 grim_message

 ;--------------------------------
 ; hide points
 ;--------------------------------
 pg_hide, reflection_ptd, cd=gd.cd, gbx=gd.pd, dkx=gd.rd, /disk, /globe, hide_ptd, /cat
 grim_message
 if(hide) then hide_ptd = objarr(nref)

 xds = active_ptd

 ;-------------------------------------------------------------------
 ; note we don't care about dimensions because reflections are not
 ; associated with any objects and they are replaced each time
 ; they are recomputed
 ;-------------------------------------------------------------------
; return, [reflection_ptd, hide_ptd]
 return, reflection_ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_reflection
;
;===============================================================================
function grim_symsize_reflection, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_reflection
;
;===============================================================================
function grim_shade_reflection, data, ptd
 return, 1.0
end
;===============================================================================



;===============================================================================
; grim_compute_ring
;
;===============================================================================
function grim_compute_ring, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 grim_message, /clear

 ;--------------------------------------------------------
 ; if no active planet, then try to determine primary
 ;--------------------------------------------------------
 pds = cor_select(active_xds, 'PLANET', /class)
; if(NOT keyword_set(pds)) then pd = get_primary(gd.cd, gd.pd) $
; else pd = (get_primary(gd.cd, pds))[0]
 if(NOT keyword_set(pds)) then pd = gd.pd $
 else pd = pds

 rds = gd.rd
 nrd = n_elements(rds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 ring_ptd = pg_disk(cd=gd.cd, dkx=rds, fov=fov, npoints=npoints)
 grim_message

 pg_hide, ring_ptd, cd=gd.cd, gbx=gd.pd, /globe, hide_ptd_globe, /cat
 grim_message
 if(hide) then hide_ptd_globe = objarr(2*nrd)

 pg_hide, ring_ptd, cd=gd.cd, od=gd.sund, gbx=gd.pd, /globe, hide_ptd_shad, /cat
 grim_message


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 if(NOT keyword_set(pnt_valid(ring_ptd))) then return, 0

; ptd = reform(ring_ptd, 2,nrd, /over)
; if(keyword_set(hide_ptd_shad)) then $
;               ptd = append_array(ptd, reform(hide_ptd_shad, 2,nrd, /over))
; if(keyword_set(hide_ptd_globe)) then $
;               ptd = append_array(ptd, reform(hide_ptd_globe, 2,nrd, /over))

 ptd = ring_ptd
 if(keyword_set(hide_ptd_shad)) then ptd = [ptd, hide_ptd_shad]
 if(keyword_set(hide_ptd_globe)) then ptd = [ptd, hide_ptd_globe]

 xds0 = reform([transpose(rds), transpose(rds)], 2*nrd)
 xds = xds0
 if(keyword_set(hide_ptd_shad)) then xds = [xds, xds0]
 if(keyword_set(hide_ptd_globe)) then xds = [xds, xds0]

 return, ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_ring
;
;===============================================================================
function grim_symsize_ring, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_ring
;
;===============================================================================
function grim_shade_ring, data, ptd

 desc = pnt_desc(ptd)
 nptd = n_elements(ptd)

 w = where(strpos(desc, 'hide_globe') NE -1)

 shade = make_array(nptd, val=1.0)
 if(w[0] NE -1) then shade[w] = 0.5

 return, shade
end
;===============================================================================



;===============================================================================
; grim_compute_ring_grid
;
;===============================================================================
function grim_compute_ring_grid, map=map, fov=fov, hide=hide, $
                             gd=gd, active_xds=active_xds, active_ptd=active_ptd, $
                             data=data, $
                             npoints=npoints, xds=xds

 grim_message, /clear

 ;--------------------------------------------------------
 ; if no active planet, then try to determine primary
 ;--------------------------------------------------------
 rds = cor_select(active_xds, 'RING', /class)
 if(NOT keyword_set(rds)) then rds = gd.rd
 if(NOT keyword_set(rds)) then return, 0

 nrd = n_elements(rds)

 ;--------------------------------
 ; compute points
 ;--------------------------------
 grid_ptd = pg_grid(cd=gd.cd, bx=rds, fov=fov, npoints=npoints)
 grim_message

 pg_hide, grid_ptd, cd=gd.cd, gbx=gd.pd, /globe, hide_ptd_globe, /cat
 grim_message
 if(hide) then hide_ptd_globe = objarr(nrd)

 pg_hide, grid_ptd, cd=gd.cd, od=gd.sund, gbx=gd.pd, /globe, hide_ptd_shad, /cat
 grim_message


 ;-------------------------------------
 ; order points correctly
 ;-------------------------------------
 if(NOT keyword_set(hide_ptd_shad)) then hide_ptd_shad = obj_new()
 if(NOT keyword_set(hide_ptd_globe)) then hide_ptd_globe = obj_new()

 ptd = [grid_ptd, hide_ptd_shad, hide_ptd_globe]
 xds = [rds, rds, rds]

 return, ptd
end
;===============================================================================



;===============================================================================
; grim_symsize_ring_grid
;
;===============================================================================
function grim_symsize_ring_grid, data
 return, -1
end
;===============================================================================



;===============================================================================
; grim_shade_ring_grid
;
;===============================================================================
function grim_shade_ring_grid, data, ptd

 desc = pnt_desc(ptd)
 nptd = n_elements(ptd)

 w = where(strpos(desc, 'hide_globe') NE -1)

 shade = make_array(nptd, val=1.0)
 if(w[0] NE -1) then shade[w] = 0.5

 return, shade
end
;===============================================================================










