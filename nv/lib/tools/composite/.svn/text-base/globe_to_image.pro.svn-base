;=============================================================================
;+
; NAME:
;       globe_to_image
;
;
; PURPOSE:
;       Translates a point in body globe coordinates to image coordinates.
;
;
; CATEGORY:
;       NV/LIB/TOOLS
;
;
; CALLING SEQUENCE:
;       result = globe_to_image(cd, gbd, p)
;
;
; ARGUMENTS:
;  INPUT:
;             cd:       Camera or map descriptor
;
;             gbx:      Object descriptor (of type GLOBE)
;
;              p:       An array of globe points
;
;  OUTPUT:
;       NONE
;
; RETURN:
;       An array of image points.
;
; STATUS:
;       Completed.
;
;
; MODIFICATION HISTORY:
;       Written by:     Spitale;
;-
;=============================================================================
function globe_to_image, cd, gbx, p, body_pts=body_pts, valid=valid

 nt = n_elements(cd)
 sv = size(p)
 nv = sv[1]

 class = class_get(cd[0])

 case class of 
  'MAP' : return, map_map_to_image(cd, globe_to_map(cd, gbx, p), valid=valid)

  'CAMERA' : $
	begin
	 body_pts = glb_globe_to_body(gbx, p)
	 image_pts = inertial_to_image_pos(cd, $
                           bod_body_to_inertial_pos(gbx, body_pts))
	 valid = in_image(cd, image_pts, slop=0)
	 return, image_pts
	end

  default :
 endcase

end
;==========================================================================
